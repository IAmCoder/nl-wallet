def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def overrideFlutterMinSdkVersion = 23
def overrideFlutterTargetSdkVersion = 34
def overrideFlutterCompileSdkVersion = 34

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def dartEnvironmentVariables = []
if (project.hasProperty('dart-defines')) {
    dartEnvironmentVariables = project.property('dart-defines')
            .split(',')
            .collectEntries { entry ->
                def pair = new String(entry.decodeBase64(), 'UTF-8').split('=')
                [(pair.first()): pair.last()]
            }
}

// Universal / deep links config
def ulHostname = dartEnvironmentVariables['UL_HOSTNAME']
def ulHostnameIsSet = ulHostname != null && !ulHostname.isEmpty()

def ulIntentFilterAutoVerify = ulHostnameIsSet
def ulIntentFilterHost = ulHostnameIsSet ? ulHostname : '*'
def ulIntentFilterPathPrefix = ulHostnameIsSet ? '/deeplink' : ''
def ulIntentFilterScheme = ulHostnameIsSet ? 'https' : 'walletdebuginteraction'

android {
    compileSdkVersion overrideFlutterCompileSdkVersion
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    ndkVersion "25.2.9519653"

    sourceSets {
        main {
            // Include native libraries
            jniLibs.srcDirs += 'src/main/jniLibs'
        }
    }

    packagingOptions {
        // Exclude the platform_support.so files that are added by the
        // platform_support module, as this code is also in libwallet_core.so
        jniLibs {
            excludes += "**/libplatform_support.so"
        }
    }

    defaultConfig {
        applicationId "nl.ictu.edi.wallet.latest"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-build-configuration.
        minSdkVersion overrideFlutterMinSdkVersion
        targetSdkVersion overrideFlutterTargetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        // Set universal & deep links intent-filter placeholders
        manifestPlaceholders['ulIntentFilterAutoVerify'] = ulIntentFilterAutoVerify
        manifestPlaceholders['ulIntentFilterHost'] = ulIntentFilterHost
        manifestPlaceholders['ulIntentFilterPathPrefix'] = ulIntentFilterPathPrefix
        manifestPlaceholders['ulIntentFilterScheme'] = ulIntentFilterScheme
    }

    signingConfigs {
        common {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file('../' + keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.common
        }
        profile {
            signingConfig signingConfigs.common
        }
        release {
            signingConfig signingConfigs.common
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters "arm64-v8a", "armeabi-v7a", "x86_64"
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "net.java.dev.jna:jna:5.7.0@aar" // Java Native Access
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation project(path: ':platform_support')
}

// Target directory for the Rust library files
def jniTargetDir = "${project.projectDir}/src/main/jniLibs"

// Register tasks to build the Rust code and copy the resulting library files
[
        Debug  : [false, null],
        Profile: [true, ['--locked', '--release']],
        Release: [true, ['--locked', '--release']]
].each {
    def taskPostfix = it.key
    def (doStrip, profileMode) = it.value
    tasks.whenTaskAdded { task ->
        if (task.name == "pre${taskPostfix}Build") {
            task.dependsOn "cargoBuildNativeLibrary$taskPostfix"
        }
    }
    tasks.register("cargoBuildNativeLibrary$taskPostfix", Exec) {
        workingDir "../../../wallet_core"

        // Build the Rust code (wallet_core)
        commandLine 'cargo', 'ndk',
                '-t', 'armeabi-v7a',
                '-t', 'arm64-v8a',
                '-t', 'x86_64',
                '-o', jniTargetDir
        if (!doStrip) {
            args '--no-strip'
        }
        args 'build',
                '-p', 'flutter_api'
        if (profileMode != null) {
            args profileMode
        }
        if (dartEnvironmentVariables['DISABLE_TLS_VALIDATION'] == 'true') {
            args '--features', 'wallet/disable_tls_validation'
        }
        if (dartEnvironmentVariables['ALLOW_HTTP_RETURN_URL'] == 'true') {
            args '--features', 'wallet/allow_http_return_url'
        }
        if (dartEnvironmentVariables['ENV_CONFIGURATION'] == 'true') {
            args '--features', 'wallet/env_config'
        }
    }
}

clean.doFirst {
    println "Cleaning $jniTargetDir"
    delete jniTargetDir
}
